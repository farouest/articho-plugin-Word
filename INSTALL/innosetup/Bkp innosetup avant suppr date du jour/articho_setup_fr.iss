; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "Articho"
#define MyAppVersion "2024.13"
#define MyAppPublisher "Mikael Bidault"
#define MyAppURL "https://articho.eu/"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E5D3F510-D906-4D45-AF5C-5308B8E24695}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher=Mikael Bidault
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={userappdata}\Microsoft\Word\STARTUP
DisableDirPage=yes
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
OutputDir=C:\Users\mikael\Documents\PRO\Articho\Install\
OutputBaseFilename=articho_setup
Compression=lzma
SolidCompression=yes
Uninstallable=yes
LicenseFile=.\licence.txt
DisableWelcomePage=no
DisableReadyPage=yes
WizardImageFile=setup1.bmp
WizardSmallImageFile=setup2.bmp
SetupIconFile=  logo64.ico
PrivilegesRequired=lowest
;InfoBeforeFile=C:\Users\Clément\Documents\articho\avant.txt                            
;InfoAfterFile=C:\Users\Clément\Documents\articho\apres.txt


[Languages]
Name: "french"; MessagesFile: "compiler:Languages\French.isl"

[Files]
Source: ".\ARTICHO\data_articho\data-en.dotx"; DestDir: "{app}\data_articho\"; Flags:  onlyifdoesntexist; BeforeInstall: sleeping; AfterInstall: sleeping;
Source: ".\ARTICHO\data_articho\data-fr.dotx"; DestDir: "{app}\data_articho\"; Flags:  onlyifdoesntexist; BeforeInstall: sleeping; AfterInstall: sleeping;
Source: ".\ARTICHO\data_articho\styles.dotx"; DestDir: "{app}\data_articho\"; Flags: ignoreversion; BeforeInstall: sleeping; AfterInstall: sleeping;
Source: ".\articho.dotm"; DestDir: "{app}"; Flags: ignoreversion; BeforeInstall: sleeping; AfterInstall: sleeping; 
Source: ".\articho.bmp"; DestDir: "{tmp}"; Flags: dontcopy nocompression
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Messages]
WelcomeLabel2=Le complément Word de la préparation de copie%n%nCe programme installera l'Articho (version {#MyAppVersion}) sur votre ordinateur.%nIl est recommandé de fermer Microsoft Word avant d'exécuter ce programme.
WelcomeLabel1=Bienvenue dans le programme d'installation de l'Articho
WizardReady=Bienvenue dans le programme d'installation de l'Articho
ReadyLabel1=Le complément Word de la préparation de copie
ReadyLabel2b=Ce programme installera l'Articho (version {#MyAppVersion}) sur votre ordinateur.%n%nIl est recommandé de fermer Microsoft Word avant d'exécuter ce programme.

[Registry]
Root: HKCU64; Subkey: "Software\VB and VBA Program Settings\Articho\Licence"; ValueType: string; ValueName: "Code"; ValueData: {code:GetDateTime} ; Flags: createvalueifdoesntexist; Check: IsWin64;     
Root: HKCU32; Subkey: "Software\VB and VBA Program Settings\Articho\Licence"; ValueType: string; ValueName: "Code"; ValueData: {code:GetDateTime} ; Flags: createvalueifdoesntexist;     
Root: HKCU64; Subkey: "Software\VB and VBA Program Settings\Articho\Licence"; ValueType: string; ValueName: "Abo"; ValueData: "eval" ; Flags: createvalueifdoesntexist; Check: IsWin64;     
Root: HKCU32; Subkey: "Software\VB and VBA Program Settings\Articho\Licence"; ValueType: string; ValueName: "Abo"; ValueData: "eval" ; Flags: createvalueifdoesntexist;     
Root: HKCU64; Subkey: "Software\VB and VBA Program Settings\Articho\Licence"; ValueType: string; ValueName: "Langue"; ValueData: "FR" ; Check: IsWin64;     
Root: HKCU32; Subkey: "Software\VB and VBA Program Settings\Articho\Licence"; ValueType: string; ValueName: "Langue"; ValueData: "FR" ;      
Root: HKCU64; Subkey: "Software\VB and VBA Program Settings\Articho\Licence"; ValueType: string; ValueName: "Version"; ValueData: {#MyAppVersion} ; Check: IsWin64;     
Root: HKCU32; Subkey: "Software\VB and VBA Program Settings\Articho\Licence"; ValueType: string; ValueName: "Version"; ValueData: {#MyAppVersion} ;      


[Code]
var                                                       
  LightMsgPage: TOutputMsgWizardPage;
function InitializeSetup: Boolean;
begin
   ExtractTemporaryFile('articho.bmp'); //here you extract your first BMP file to temp folder
   Result := True;
end;
procedure InitializeWizard;
begin
    LightMsgPage := CreateOutputMsgPage(wpInstalling,
    '', '',
    'L''Articho est maintenant installé sur votre ordinateur !'#13#13 +
    'Si Word était ouvert au moment de l''installation, vous devez quitter l''application et la relancer afin d''activer l''Articho.'#13#13 +
    'Pour accéder aux fonctions de l''Articho, cliquez sur l''onglet Articho du ruban, à droite de l''onglet Affichage.'#13#13 +
    'En cas de difficulté, n''hésitez pas à nous envoyer un email à contact@articho.eu');
end;
procedure sleeping();
begin
  Sleep(2000);
end;
procedure CurPageChanged(CurPageID: Integer);
var
BmpFile1 : TBitmapImage;
begin
  if CurPageID = wpInstalling then begin 
      BmpFile1:= TBitmapImage.Create(WizardForm);
      BmpFile1.Bitmap.LoadFromFile(ExpandConstant('{tmp}\articho.bmp'));
      BmpFile1.Width:= ScaleX(450);
      BmpFile1.Height:= ScaleY(225);
      BmpFile1.Stretch := True;
      BmpFile1.AutoSize := True;
      BmpFile1.Center := True;
      BmpFile1.Left := WizardForm.ProgressGauge.Left + ScaleX(0); 
      BmpFile1.Top := WizardForm.ProgressGauge.Top + ScaleY(35);
      BmpFile1.Parent:= WizardForm.InstallingPage;
      
  end;
end;
function GetDateTime(dummy: String): String;
begin
  Result := GetDateTimeString('dd/mm/yyyy', '-', '-');
end;